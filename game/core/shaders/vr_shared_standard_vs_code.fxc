// Copyright (c) Valve Corporation, All rights reserved. ======================================================================================================
//-------------------------------------------------------------------------------------------------------------------------------------------------------------
//
// These functions are an attempt to standardize the lighting models across HLVR scene shaders, with the goal of accelerating shader authoring and
// homogenizing lighting calculations and making code extension easier.
//
// VS_SharedStandardProcessing should be called at the top of all vertex shaders and sets up a common structure that is used to communicate to our pixel shader input
//
//-------------------------------------------------------------------------------------------------------------------------------------------------------------

#ifndef VR_SHARED_VS_CODE_FXC
#define VR_SHARED_VS_CODE_FXC

#include "vr_common_vs_code.fxc"
#include "vr_detail_texture.fxc"

//-------------------------------------------------------------------------------------------------------------------------------------------------------------
//
// Common Vertex Shader Combos
//
//-------------------------------------------------------------------------------------------------------------------------------------------------------------
DynamicCombo( D_BAKED_LIGHTING_FROM_LIGHTMAP, 0..1, Sys( ALL ) );

//
// Constants 
//
float2 g_vTexCoordScale < Default2( 1.0, 1.0 ); Range2( 0.0, 0.0, 100.0, 100.0 ); UiGroup( "Texture Coordinates,80/10" ); >;
float2 g_vTexCoordOffset < Default2( 0.0, 0.0 ); Range2( -1.0, -1.0, 1.0, 1.0 ); UiGroup( "Texture Coordinates/20" ); >;
float2 g_vTexCoordScrollSpeed < Default2( 0.0, 0.0 ); Range2( -10.0, -10.0, 10.0, 10.0 ); UiGroup( "Texture Coordinates/30" ); >;

#if ( !S_LIQUID_SHADING && !D_NO_MODEL_TINT )
	float3 g_vColorTint < UiType( Color ); Default3( 1.0, 1.0, 1.0 ); UiGroup( "Color,10/20" ); >;
	float g_flModelTintAmount < Default( 1.0 ); Range( 0.0, 1.0 ); UiGroup( "Color,10/30" ); > ;
	float g_flFadeExponent < Default( 1.0 ); Range( 1.0, 16.0 ); UiGroup( "Fade,400/10" ); >;
	Float3Attribute( LightSim_MaterialTint, g_vColorTint );
#endif

bool g_bUseSecondaryUvForDetailTexture < Default( 1.0 ); UiGroup( "Detail Texture, 170" ); >;

//-------------------------------------------------------------------------------------------------------------------------------------------------------------
//
// Common Vertex Processing Code
//
//-------------------------------------------------------------------------------------------------------------------------------------------------------------
#ifdef SHARED_STANDARD_VS_INPUT_DEFINED

PS_INPUT VS_SharedStandardProcessing( const VS_INPUT i )
{
	PS_INPUT o;

	o = VS_CommonProcessing( i );

	//
	// Detail texturing
	//
	#if ( S_DETAIL_TEXTURE )
	{
		float2 vDetailTextureCoords = GetUVDefault1( g_bUseSecondaryUvForDetailTexture, o.vTextureCoords );
		o.vDetailTextureCoords.xy = CalculateDetailTextureCoordinates( vDetailTextureCoords.xy );
	}
	#endif

	//
	// Create texture coords
	//
	o.vTextureCoords.xy = o.vTextureCoords.xy * g_vTexCoordScale.xy + g_vTexCoordOffset.xy;

	// Scrolling uv's
	o.vTextureCoords.xy += g_vTexCoordScrollSpeed.xy * g_flTime;

	#if ( !S_LIQUID_SHADING && !D_NO_MODEL_TINT )
	{
		//
		// Model tint amount
		//
		o.vVertexColor.rgb = lerp( float3( 1.0, 1.0, 1.0 ), o.vVertexColor.rgb, g_flModelTintAmount );
		o.vVertexColor.rgb *= SrgbGammaToLinear( g_vColorTint.rgb );


		//
		// Fade Exponent
		//
		o.vVertexColor.a = pow( o.vVertexColor.a, g_flFadeExponent );
	}
	#endif

	return o;
}

#endif

//-------------------------------------------------------------------------------------------------------------------------------------------------------------

#endif
