// Copyright (c) Valve Corporation, All rights reserved. ======================================================================================================

#ifndef GRADIENT_FOG_FXC
#define GRADIENT_FOG_FXC

// Includes ---------------------------------------------------------------------------------------------------------------------------------------------------
#include "system.fxc"
#include "vr_common.fxc"
#include "sky.fxc"

//-------------------------------------------------------------------------------------------------------------------------------------------------------------

float4 CalculateGradientFog( float3 vPositionWs, float3 vPositionToCameraWs )
{
	float2 vGradientFogDistance = float2( length( vPositionToCameraWs.xy ), vPositionWs.z );

	float2 vGradientFogNormalizedRange = saturate( g_vGradientFogBiasAndScale.xy + g_vGradientFogBiasAndScale.zw * vGradientFogDistance.xy );
	float flVerticalLevel = pow( vGradientFogNormalizedRange.y, m_vGradientFogExponents.y );
	float flFogLevel = pow( vGradientFogNormalizedRange.x, m_vGradientFogExponents.x ) * flVerticalLevel * g_vGradientFogColor_Opacity.w;
	return float4( g_vGradientFogColor_Opacity.rgb, flFogLevel );
}

//-------------------------------------------------------------------------------------------------------------------------------------------------------------

float3 ApplyGradientFog( float3 vInputColor, float3 vPositionWs, float3 vPositionToCameraWs )
{
	// Check if we're going to have any fog at all (near distance will be Inf if fog is off)
	float fogDistSqr = dot( vPositionToCameraWs.xy, vPositionToCameraWs.xy );
	// this is generally flattened, but since the gradient fog is often far out, experimenting with forcing a branch.
	[branch] 
	if ( fogDistSqr > g_vGradientFogCullingParams.x && vPositionWs.z < g_vGradientFogCullingParams.y )
	{
		float4 vGradFog = CalculateGradientFog( vPositionWs, vPositionToCameraWs );

		return lerp( vInputColor.rgb, vGradFog.rgb, vGradFog.a );
	}
	return vInputColor;
}

//-------------------------------------------------------------------------------------------------------------------------------------------------------------

#endif // GRADIENT_FOG_FXC
